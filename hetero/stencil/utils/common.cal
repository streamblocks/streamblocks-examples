namespace hetero.stencil.utils.common:

  @pipeline(flushing=true)
  actor Filter(
    uint x_dim, uint y_dim,
    uint x_lb, uint x_ub, 
    uint y_lb, uint y_ub, 
    int x_offset, 
    int y_offset
    ) 
    int In ==> int Out:
    uint x_index := 0; 
    uint y_index := 0;
    /**
      icrement the y and x indices and wrap around if necessary
    */
    procedure increment_index() 
    begin
      if y_index = y_dim - 1 then
        y_index := 0;
        if x_index = x_dim - 1 then
          x_index := 0;
        else
          x_index := x_index + 1;
        end
      else
        y_index := y_index + 1;
      end 
    end

    /**
      Drop the input token
     */
    drop: action In:[t] ==> 
    do
      increment_index();
    end

    /**
      Pass the input token to the output
     */
    pass: action In:[t] ==> Out:[t]
    guard x_index >= x_offset + x_lb and x_index <= x_offset + x_ub and 
          y_index >= y_offset + y_lb and y_index <= y_offset + y_ub
    do
      increment_index();
    end
    

    priority
      pass > drop;
    end

  end

  @pipeline(flushing=true)
  actor Splitter() int In ==> int Left, int Right:
    split: action In:[t] ==> Left:[t], Right:[t] end
  end

  

end