namespace hetero.stencil.utils.formats:

   import all System.bitops;

   actor YCrCb420to422(int dynamic_size, int picture_width)
         int(size=dynamic_size) CrCb420
         ==>
         int(size=dynamic_size) CrCb422
         :
         
         
         function mul(int(size=18)a,int(size=18)b) --> int(size=36): a*b end 
         
                  
         List(type: int(size = dynamic_size), size=picture_width) qLines0;
         List(type: int(size = dynamic_size), size=picture_width) qLines1;
         

            
         lateness:action
            CrCb420:[q]  repeat picture_width
            ==>
         do
         qLines1:=q;
         end
         
         fire_phase1:action
            CrCb420:[q] repeat picture_width
            ==>
            CrCb422:[[rshift(mul(qLines0[k],3)+q[k],2) : for int k in 0..(picture_width - 1)]]  repeat picture_width
         do
            qLines0:=qLines1;
            qLines1:=q;
         end
         
         fire_phase2:action
            ==>
            CrCb422:[[rshift(mul(qLines1[k],3)+qLines0[k],2):for int k in 0..(picture_width - 1)]]  repeat picture_width
         end
         
         schedule fsm A:
         A(lateness)	  --> B;
         B(fire_phase1)--> C;
         C(fire_phase2)--> B;
      endschedule
   end
end