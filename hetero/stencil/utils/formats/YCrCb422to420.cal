namespace hetero.stencil.utils.formats:

  import all System.bitops;

	actor YCrCb422to420(int dynamic_size, int picture_width)
					int(size=dynamic_size) CrCb422
					==>
					int(size=dynamic_size) CrCb420
					:
				
			function initList(int v, int _sz): [v : for i in 0..(_sz - 1)] end
			function mul(int(size=18) a,int(size=18) b): a*b end 
			function tap(a,b,c,d): [rshift(a[k]+mul(b[k],3)+mul(c[k],3)+d[k],3):for k in 0 .. (picture_width - 1)] end
				
			List(type: int(size=dynamic_size), size=picture_width) crcbLines0;
			List(type: int(size=dynamic_size), size=picture_width) crcbLines1;
			List(type: int(size=dynamic_size), size=picture_width) crcbLines2;
			
			lateness:action
				CrCb422:[crcb] repeat picture_width
				==>
			do
				crcbLines0:=crcbLines1;
				crcbLines1:=crcbLines2;
				crcbLines2:=crcb;
			end
					
			fire:action
				CrCb422:[crcb] repeat picture_width
				==>
				CrCb420:[tap(crcbLines0,crcbLines1,crcbLines2,crcb)] repeat picture_width
			do
				crcbLines0:=crcbLines1;
				crcbLines1:=crcbLines2;
				crcbLines2:=crcb;
			end
					
			schedule fsm A:
					A(lateness)	--> B;
					B(fire)		--> A;
			endschedule
	end
end