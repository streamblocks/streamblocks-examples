// splitter_BTYPE.cal
//
// Author: Christophe Lucarz (christophe.lucarz@epfl.ch) 
// 
// Description: the sequence of BTYPE token is not homogeneous, 
// there is fist a NEW VOP command, then, the width and height of 
// this VOP, then the  corresponding 8x8 blocks of texture, motion or
// mixed tex/mot. This actor will delete the tokens concerning the 
// information of the VOP and redistribute the tokens to the Y, U and V components. 
// So that the adder have directly the token intra, accoded or mixed.

namespace RVC:

import System.bitops.bitand;

 actor splitter_BTYPE(int BTYPE_SZ, int NEWVOP) int(size=BTYPE_SZ) BTYPE ==> int(size=BTYPE_SZ) Y , int(size=BTYPE_SZ) U, int(size=BTYPE_SZ) V :

   // to send the newvop token to the address and add block (arrggg) 
   cmd.newVop : action BTYPE:[cmd] ==> Y:[ cmd ] , U:[ cmd ], V:[ cmd ]
   guard
     bitand(cmd,NEWVOP) != 0
   end 

   // to delete the token relative to the width and the height
   skip : action BTYPE:[cmd0, cmd1]  ==> Y:[ cmd0, cmd1 ]  , U:[ cmd0, cmd1 ], V:[cmd0, cmd1]
   end

   // to split the tokens 
   cmd.split: action BTYPE:[y0, y1, y2, y3, u, v]  ==> Y:[ y0, y1, y2, y3 ] , U:[ u ], V:[ v ]
   end 

   schedule fsm cmd:
     cmd   (cmd.newVop)  --> skip;
     cmd   (cmd.split )  --> cmd;
     skip  (skip  )  --> cmd;
   end 

   priority 
     cmd.newVop > cmd.split;
   end
 end
end